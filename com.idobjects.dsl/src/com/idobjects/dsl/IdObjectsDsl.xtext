grammar com.idobjects.dsl.IdObjectsDsl with org.eclipse.xtext.common.Terminals

generate idObjectsDsl "http://www.idobjects.com/IdObjectsDsl"

Model:
	'model-id' '=' name=PackageName	
	packages+=Package*
	(persistenceModel=Persistence)?
;
Package:
	'package' name=PackageName '{'
	entities+=Entity*
	'}';

Entity:
	'entity' name=ID '{'
	(properties+=EntityProperty | references+=EntityReference)*
	'}';

EntityReference:
	('List[' listDestination=[Entity] ']'
	|
	singleDestination=[Entity])
	name=ID
	('[inverse=' inverseName=ID ']')?
	';';

Persistence: 'persistence' '{'
	persistencePackage+=PersistencePackage+
	'}'
	;

PersistencePackage:
	'package' name=PackageName '{'
	persistenceEntities+=PersistenceEntity+
	'}';

PersistenceEntity:
	'entity' name=ID '{'
	persistenceProperties+=PersistenceProperty+
	'}';

PersistenceProperty:
	name=ID '('
		config += PersistencePropertyConfig ( ',' config +=PersistencePropertyConfig)*
	')';
	
PersistencePropertyConfig:
	propertyConfigKey=ID '=' propertyConfigValue = STRING
;


EntityProperty:
	propertyType=BasicType name=ID ';';

PackageName:
	ID ('.' ID)*;

BasicType:
	PrimitiveTypes | WrapperTypes | DirectSupportedTypes | JodaTimeTypes;

JodaTimeTypes:
	'DateTime' | 'Instant';

DirectSupportedTypes:
	'String';

WrapperTypes:
	'Byte' | 'Short' | 'Integer' | 'Long' | 'Float' | 'Double' | 'Boolean' | 'Character';

PrimitiveTypes:
	'byte' | 'short' | 'int' | 'long' | 'float' | 'double' | 'boolean' | 'char';

